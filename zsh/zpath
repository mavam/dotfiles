#
# zpath - Sets PATH related variables.
#

# Automatically remove duplicates from these arrays.
typeset -U path cdpath fpath manpath

# System $PATH.
path=(
  /usr/local/bin
  /usr/bin
  /bin
  /usr/local/sbin
  /usr/sbin
  /sbin
  /usr/X11R6/bin
  /usr/ucb            # SunOS
  /usr/sww/bin        # SunOS
)

# Man pages.
manpath=(
  /usr/local/man
  /usr/man
)

# OS-specific paths.
case $OSTYPE in
  darwin*)
    # MacTeX binaries.
    path=( $path /usr/texbin )
    path=( $path /Library/Tex/texbin )
    # Apple's X11 binaries.
    path=( $path /usr/X11R6/bin )
    # Default MacPorts installations.
    path=( /opt/local/bin /opt/local/sbin $path )
    manpath=( /opt/local/share/man $manpath )
    # Common Homebrew paths
    path=(
      /usr/local/opt/llvm/bin     # brew --prefix llvm
      /usr/local/opt/ruby/bin     # brew --prefix ruby
      /usr/local/opt/python/bin   # brew --prefix python
      $path
    )
    ;;
esac

# Go
export GOPATH="$HOME/.go"
path=( "$GOPATH/bin" $path )

# Rust
path=( ~/.cargo/bin $path )

# Source local paths.
if [[ -f ~/.zpath.local ]]; then
  source ~/.zpath.local
fi

# Home prefix has highest precedence, thus sourced last.
path=( ~/bin $path )
manpath=( ~/man $manpath )
if [[ -d ~/.gem && -d ~/.gem/ruby ]]; then
  foreach dir (~/.gem/ruby/*) { path=( $dir/bin $path ) }
fi

# Removes non-existent directories from an array.
clean-path () {
  local element
  local build
  build=()
  # Make sure that this works even with variables containing IFS characters,
  # if you're crazy enough to setopt shwordsplit.
  eval '
  foreach element in "$'"$1"'[@]"; do
      [[ -d "$element" ]] && build=("$build[@]" "$element")
  done
  '"$1"'=( "$build[@]" )
  '
}

# Finally, clean the path variables.
clean-path path
clean-path manpath
clean-path fpath

# vim: ft=zsh

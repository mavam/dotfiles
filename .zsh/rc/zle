## 
##  Command Line Editing 
## 

# To find out the keynames you can use
#  % cat > /dev/null 
#  or
#  % od -c

# that's the good guy <:
bindkey -v

# Ensures that I can delete over stuff that was done in a previous insert
# session, not proper vi action
bindkey -M viins '' backward-delete-char
bindkey -M viins '' backward-delete-char
stty ek

case $TERM in
	screen*|linux*|xterm*)
	bindkey "^[[1~"  beginning-of-line	# Home
	bindkey "^[[4~"  end-of-line		# End
	;;
	rxvt|(dt|k|E)term)
	bindkey "^[[7~" beginning-of-line
	bindkey "^[[8~" end-of-line
	;;
esac

bindkey "^[[2~" yank					# Insert
bindkey "^[[3~" delete-char				# Delete
bindkey "^[[5~" history-search-backward	# PgUp
bindkey	"^[[6~" history-search-forward	# PgDn
bindkey "^[[A" up-line-or-search		# up arrow: back-history-search
bindkey "^[[B" down-line-or-search		# down arrow: fwd-history-search

bindkey "^[e" expand-cmd-path			# C-e for expanding path of typed cmd
bindkey " " magic-space					# do history expansion on space
bindkey "^@" push-line					# C-<space> push line on buffer stack
bindkey "^k" run-help					# C-k: man <...>
bindkey "^v" quoted-insert

# Emacs compatibility
bindkey "^y" yank                       # C-y: Insert kill buffer contents
bindkey "^n" down-history				# C-n: next command in history
bindkey "^p" up-history					# C-p: prev command in history
bindkey "^a" beginning-of-line			# C-a: jump to the beginning of line
bindkey "^e" end-of-line				# C-e: jump to end of line
bindkey "^u" kill-whole-line			# C-u: kill whole line
bindkey "f" forward-word				# Alt-f: forward word
bindkey "b" backward-word				# Alt-b: backward word

magic-space-rationalise-dot() {
    case "$KEYS" in
        (" ") zle .magic-space "$@" ;;
        ( . ) [[ $LBUFFER = *.. ]] && LBUFFER+=/. ;&
        ( * ) zle .self-insert "$@" ;;
    esac
}
zle -N magic-space magic-space-rationalise-dot
bindkey "." magic-space

# Keep failing searches on the command line.
_failsafe_hisb()
{
    if is-at-least 4.3.9; then
        zle history-incremental-pattern-search-backward
    else
        zle history-incremental-search-backward
    fi

    [[ $? = 3 ]] && BUFFER=$LASTABORTEDSEARCH
}
zle -N _failsafe_hisb
bindkey "^r" _failsafe_hisb

# Insert the current date (yyyy-mm-dd) 
_insert_date() { BUFFER="$BUFFER$(date '+%F')"; CURSOR=$#BUFFER }
zle -N _insert_date
bindkey "^Xd" _insert_date

# Sudo the command line.
_run_with_sudo() { LBUFFER="sudo $LBUFFER"; CURSOR=$#BUFFER }
zle -N _run_with_sudo
bindkey "^Xs" _run_with_sudo

zle -N incremental-complete-word
bindkey "^Xi" incremental-complete-word 

zle -N insert-files
bindkey "^Xf" insert-files

# vim: ft=zsh
